extern exception_handler_cpp

%macro ISR_NOERRCODE 1
  global isr%1
  isr%1:
    push 0                  ; Push dummy error code
    push %1                 ; Push interrupt number
    jmp common_isr_handler
%endmacro

%macro ISR_ERRCODE 1
  global isr%1
  isr%1:
    push %1                 ; Push interrupt number
    jmp common_isr_handler
%endmacro

common_isr_handler:
  ; Save CPU state
  push rbp
  push rax
  push rbx
  push rcx
  push rdx
  push rsi
  push rdi
  push r8
  push r9
  push r10
  push r11
  push r12
  push r13
  push r14
  push r15

  mov rdi, [rsp + 120]      ; Argument 1: Vector number (pushed before common_isr_handler)
  mov rsi, rsp              ; Argument 2: Pointer to InterruptFrame (current stack pointer)

  call exception_handler_cpp

  ; Restore CPU state
  pop r15
  pop r14
  pop r13
  pop r12
  pop r11
  pop r10
  pop r9
  pop r8
  pop rdi
  pop rsi
  pop rdx
  pop rcx
  pop rbx
  pop rax
  pop rbp

  add rsp, 16               ; Clean up error code and vector number
  iretq

; Define ISRs
ISR_NOERRCODE 0
ISR_NOERRCODE 1
ISR_NOERRCODE 2
ISR_NOERRCODE 3
ISR_NOERRCODE 4
ISR_NOERRCODE 5
ISR_NOERRCODE 6
ISR_NOERRCODE 7
ISR_ERRCODE   8
ISR_NOERRCODE 9
ISR_ERRCODE   10
ISR_ERRCODE   11
ISR_ERRCODE   12
ISR_ERRCODE   13
ISR_ERRCODE   14
ISR_NOERRCODE 15
ISR_NOERRCODE 16
ISR_ERRCODE   17
ISR_NOERRCODE 18
ISR_NOERRCODE 19
ISR_NOERRCODE 20
ISR_NOERRCODE 21
ISR_NOERRCODE 22
ISR_NOERRCODE 23
ISR_NOERRCODE 24
ISR_NOERRCODE 25
ISR_NOERRCODE 26
ISR_NOERRCODE 27
ISR_NOERRCODE 28
ISR_NOERRCODE 29
ISR_ERRCODE   30
ISR_NOERRCODE 31
ISR_NOERRCODE 32
ISR_NOERRCODE 33
ISR_NOERRCODE 34
ISR_NOERRCODE 44

